config {
    type: "incremental",
    uniqueKey: ["commit_hash", "file"],
    assertions: {
        uniqueKey: ["commit_hash", "file"],
        rowConditions: [
            "author_email LIKE '%@%.%'"
        ]
    }
}

SELECT
    commit_hash,
    commit_subject,
    author_name,
    author_email,
    TIMESTAMP_SECONDS(author_date) AS author_date,
    commiter_email,
    commiter_name,
    file,
    ${regexp_functions.parseType("file")} AS type,
    ${regexp_functions.parseSubRepo("file")} AS sub_repo,
    CONCAT(author_name, ' <', author_email, '>') AS author
FROM ${ref("raw")}
${when(incremental(), `WHERE TIMESTAMP_SECONDS(author_date) > (SELECT MAX(author_date) FROM ${self()})`) }
